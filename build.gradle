plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
    mavenLocal()
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_api_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${project.cardinal_api_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_api_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${project.cardinal_api_version}"

    modImplementation project(":bridge:refabslab-api")
    include project(":bridge:refabslab-api")

//    modImplementation project(":bridge:refabslab-1-19")
//    include project(":bridge:refabslab-1-19")
    modImplementation "lv.cebbys.mcmods:refabslab-1-19:1.0.0"
    include "lv.cebbys.mcmods:refabslab-1-19:1.0.0"

    modImplementation "lv.cebbys.mcmods:celib:${project.celib_version}"
    modImplementation "lv.cebbys.mcmods:respro:${project.respro_version}"

    implementation 'org.apache.commons:commons-text:1.10.0'

    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
    testCompileOnly "org.projectlombok:lombok:${project.lombok_version}"
    testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
}

processResources {
    var map = Map.of(
            "version", project.mod_version,
            "minecraft_dependency", ">=1.16",
            "celib_dependency", project.celib_version,
            "respro_dependency", project.respro_version,
            "cardinal_api_dependency", getModDependency(project.cardinal_api_version)
    )

    inputs.properties(map)

    filesMatching("fabric.mod.json") {
        expand(map)
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

String getModDependency(Object versionObject) {
    String version = versionObject.toString()
    try {
        StringBuilder out = new StringBuilder()
        out.append(version.substring(0, version.indexOf(".") + 1))
        version = version.substring(version.indexOf(".") + 1)
        if (version.contains(".")) version = version.substring(0, version.indexOf(".") + 1)
        out.append(version).append("x")
        return out
    } catch (Exception e) {
    }
    throw new RuntimeException("Invalid mod version: " + version);
}

subprojects {
    task generateFabricModJson() {
        doLast {
            String message = "Generating '" + project.name + "' fabric.mod.json"
            System.out.println(message)
        }
    }
    compileJava {
        dependsOn tasks.named("generateFabricModJson")
    }
}